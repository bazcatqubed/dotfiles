#!/usr/bin/env -S nu --stdin

# SPDX-FileCopyrightText: 2025 Gabriel Arazas <foodogsquared@foodogsquared.one>
#
# SPDX-License-Identifier: MIT

# Annotate all of the given files in the current Git directory with all of its
# available metadata within the blob objects.
#
# This is a naive implementation that is best assumed to be run in a
# single-person project with consistent metadata in its Git history.
def main [
    ...paths,

    --reuse-args: list<string> = [ ], # Additional arguments to be passed to the generated `reuse` command.

    --contributor: string, # Override the contributor.

    --license: string, # The license to be assigned to the files.

    --style: string, # The comment style to be used.

    --dry-run, # Prints the generated commands instead of executing it.

    --json, # Generates a JSON object usable for external scripts.

    --force-current-date = false, # Force the current date for the end year of the range.
] {
  let commands = $paths | par-each { |f|
    # TODO: Get signed-off and co-authored contributors
    let contributors = ^git log --pretty="%H\t%aI\t%aN <%aE>" -- $f | lines
    | split column "\t" hash date author
    | default --empty [
      {
        date: (date now | format date),
        author: $"(^git config user.name) <(^git config user.email)>"
      }
    ]
    | group-by author
    | update cells { |v|
        $v | get date
        | append (if $force_current_date { [ (date now) ] } else { [ ] })
        | get (($in | length) - 1) 0
        | each { |d| $d | date from-human | format date "%Y" }
        | uniq
        | str join '-'
    }
    | sort --values

    let commands = $contributors | items { |k, v|
      mut cmd = [
        "reuse" "annotate"
        "--copyright" $k "--year" $v
        ...$reuse_args
      ]

      if $license != null {
        $cmd ++= [ "--license" $license ]
      }

      if $style != null {
        $cmd ++= [ "--style" $style ]
      }

      $cmd ++= [ $f ]

      $cmd
    }

    if $dry_run {
      for cmd in $commands {
        print ($cmd | str join ' ')
      }
    } else if $json {
      $commands
    } else {
      for cmd in $commands {
        run-external ...$cmd
      }
    }
  }

  if $json {
    $commands | to json
  }
}
