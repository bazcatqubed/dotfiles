#!/usr/bin/env nu

# SPDX-FileCopyrightText: 2021-2025 Gabriel Arazas <foodogsquared@foodogsquared.one>
#
# SPDX-License-Identifier: MIT

# A quick command line interface for creating a gitignore with the API from https://gitignore.io.
# This script comes with a simple caching to avoid creating too much requests.
def main [] {
  # Check if the language list is downloaded for the last hour (3600 seconds).
  mut gitignore_object = { }

  let cache_file = $'($env.XDG_CACHE_HOME? | default $'($env.HOME)/.cache')/gitignore-io.langs.json'
  if ($cache_file | path exists) {
    let cache_file = ls $cache_file
    if ((date now) - $cache_file.modified.0) < 1wk {
      $gitignore_object = open $cache_file.name.0
    }
  } else {
    $gitignore_object = http get "https://gitignore.io/api/list?format=json"
    $gitignore_object | save --force $cache_file
  }

  $gitignore_object | values | sort-by key
  | input list "Languages: " --multi --display key
  | reduce --fold "" { |it, acc| $acc + $it.contents }
}
